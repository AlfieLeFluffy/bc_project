~ start
if DialogueVariables.check_variable("dispatch_first_meet") != true
	=> first_talk
elif DialogueVariables.check_variable("dispatch_reconnected") != true
	=> call_reconnected
=> END

~ first_talk

*Incoming call from Dispatch*
Dispatch: Long time now see, detective.
Dispatch: I hope that your... "vacation", was worth all the trouble it caused.
- *Ingnore the remark*
	Dispatch: [#mood=happy] Silence, someone got their tounge snipped.
	Dispatch: Anyways, I see you arrived at the scene, let's test the device.
- There is nothing to talk about.
	Detective: There is nothing to talk about.
	Dispatch: Of course, we already know the details.
	- *Ingnore the remark*
	- Well, at least I can get a vacation.
		Detective: [#mood=happy] Well, at least I get can a get a vacation.
		Dispatch: [#mood=angry] Well, at least we don't need to commit a crime to get a break.
		do DialogueVariables.set_variable("angered_dispatch", true)
		Detective: [#mood=happy] Dispatch, I did not miss you at all.
		Detective: Anyways I am at the scene, can you finally explain the device I am supposed to test.
		Dispatch: ... *sigh*, let's get to it.
Dispatch: I am going to power on the device.
do Signals.s_SetTimelineControllerActive.emit(true)
*The device on your wrist hums to life*
Dispatch: You should now see the timeline information on the device's screen.
Dispatch: You should also see a couple of buttons.
Dispatch: Don't touch them yet.
Dispatch: I am going to do a test run of the device and send you to a timeline the device found as a closely resembling this one.
Detective: Wait, I though it was just a fancy tablet?
Detective: You are going to send me to another timeline?
Detective: Did someone test it beforehand?
Dispatch: ... you are the one that is going to test it.
- What? Wait, what if it doesn't work?
	Detective: What? Wait, what if it doesn't work?
	Dispatch: Well, you will find out.
- Can we test it on a dog or something?
	Detective: Can we test it on a dog or something?
	Dispatch: A dog cannot initiate the jump back.
Dispatch: Once you arrive at the target timeline press the "Q" button on your device.
*Initiating first timeline shift*
- Fuck you dispatch.
	Detective: [#mood=angry] Fuck you dispatch.
	Dispatch: Well, fuck you to...
- Which one again?
	Detective: [#mood=sad] Which one again?
	Dispatch: The "Q" one...
do DialogueVariables.set_variable("dispatch_first_meet", true)
do Signals.s_ShiftToTimeline.emit("002")

=> END

~ call_reconnected

*Call reconnected*
Dispatch: Finally, you are back. Did you forget it was the "Q" one?
- Yes.
	Detective: Yes.
- No.
	Detective: No.
- Fuck you again.
	Detective: [#mood=angry]Fuck you.
Dispatch: Well, you are in one piece, so it could not have been that bad.
Dispatch: Now listen, there are three other things it does and you will most probably need them.
Dispatch: First if instead of pushing the 'Q' button you hold it down it will show you all available timelines and you can select which one you want to visit or foresee.
Dispatch: Foresight is the third thing that might come in handy.
Dispatch: If you hold the "LMB" (Left Mouse Button) the device will display a view of another timeline. 
Dispatch: This can be rather helpful. The device checks if the space you want to shift into is not occupied, but it doesn't check that if you will survive it.
Dispatch: It can conclude there is free space and then you fall into a pit of spikes.
Dispatch: Lastly the device can hold all the evidence and clues you collect.
Dispatch: It has a digital pin-up board and you can thing to it with the "F" key.
Dispatch: The device is not perfect so it won't let you add everything, but it should be enough for your case.
Dispatch: [#board=Showcase]You can even add dialogue like this one.
Dispatch: Also I forgot, there is one more things and it is relevant.
Dispatch: If you forget what which key does you can hold down the "Alt" key and it will show.
- What if I already forgot?
	Detective: What if I already forgot?
	Dispatch: Then there is no hope and you will never find out. But seriously, the "Alt" key.
- Ok, let's take it out on a spin.
	Detective: Ok, let's take it out on a spin.
Dispatch: We have selected what looks an easy case for you to start with.
Dispatch: An officer is already waiting on the scene for you to arrive.
Dispatch: Dispatch out. 

do DialogueVariables.set_variable("dispatch_reconnected", true)

=> END